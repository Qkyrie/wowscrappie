buildscript {
    ext {
        springBootVersion = theSpringBootVersion
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "http://repo.spring.io/release" }
        maven { url "http://clojars.org/repo" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE"
        classpath 'de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'checkstyle'
apply plugin: 'lesscss'
apply plugin: "io.spring.dependency-management"

sourceCompatibility = 1.8
targetCompatibility = 1.8


allprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }


    checkstyle {
        ignoreFailures = true
        configFile = configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }
}

subprojects {
    group = 'com.de-swaef.scrappie'
}


group = 'com.de-swaef.scrappie'

jar {
    baseName = 'Scrappie'
    version = '2.4.0-SNAPSHOT'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://clojars.org/repo" }
    maven { url "http://repo.spring.io/milestone" }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-netflix:1.0.7.RELEASE'
        mavenBom 'io.spring.platform:platform-bom:2.0.3.RELEASE'
    }
}

dependencies {

    //own projects
    compile project(":heureka")
    compile project(":core")

    //web
    compile("org.springframework.boot:spring-boot-starter")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-websocket")
    compile("org.springframework:spring-messaging")
    compile('net.sourceforge.htmlunit:htmlunit:2.16')


    //data
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-batch")
    compile('org.springframework.data:spring-data-redis')
    compile("org.flywaydb:flyway-core")
    testCompile("com.h2database:h2")
    compile("mysql:mysql-connector-java")


    //social
    compile("org.springframework.social:spring-social-core:1.1.0.RELEASE")
    compile("org.springframework.social:spring-social-config:1.1.0.RELEASE")

    //security
    compile("org.springframework.security:spring-security-data")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.security:spring-security-web")
    compile("org.springframework.security:spring-security-config")
    compile("org.springframework.security:spring-security-messaging")
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4:2.1.2.RELEASE'
    testCompile("org.springframework.security:spring-security-test")



    //configuration
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.session:spring-session")


    //spring cloud
    compile 'org.springframework.cloud:spring-cloud-starter-eureka'
    compile("org.springframework.boot:spring-boot-starter-cloud-connectors")


    //utils
    compile('commons-validator:commons-validator:1.4.0')
    compile('org.projectlombok:lombok:1.16.6')

    //testing
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile('org.easytesting:fest-assert:1.4')
}

task "forceTest" {
    dependsOn "clean", "cleanTest", "test"
}

task "fullBuild" {
    dependsOn "clean", "cleanTest", "test", "check", "build"
}

lesscss {
    source = fileTree('src/main/resources/static/assets/less') {
        include 'sb-admin-2.less'
    }
    dest = 'src/main/resources/static/assets/css'
    compress = true
}

bootRun {
    systemProperties System.properties
}